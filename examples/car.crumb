(use "./colors.crumb" {

  // returns a single frame of the animation
  frame = {n ->

    // message to display
    message = "                HELLO WORLD                "

    // the visible part of the message, as a list
    message = (map (range n) {item i -> <- (get message i)})

    // convert back to string
    message = (reduce message {acc item i -> <- (join acc item)} "")

    // get ansi color
    c = (add 31 (remainder n 7))

    // add car
    <- (color c (join message "ō͡≡o"))
  }

  // init
  // \e[H goes to top left
  // \e[2J clears screen
  (print "\e[H\e[2J")

  // render loop
  (until "" {state n ->

    // calculate frame number
    frame_number = (add (remainder n 40) 1)

    // clear when animation loop
    (if (is frame_number 1) {(print "\e[H\e[J")})

    // \e[H goes to top left
    (print (join "\e[H" "Frame: " (string frame_number) "\n\n"))

    // \e[0m resets color
    (print (join (frame frame_number) "\n"))

    // print floor
    (print "‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾\n")

    // wait to slow down animation
    (wait 0.1)
  })

})